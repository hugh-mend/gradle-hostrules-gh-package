plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'    
    id 'java-library'
    // Enables publishing artifacts during build
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_11

group 'mend'
version '2.0.1'

repositories {
    mavenCentral()
}

//When building a multi-module project which includes a module outside of the springboot project, then the module's build.gradle must contain
bootJar {
    enabled = false
}
jar {
    enabled = true
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	implementation 'org.springframework.boot:spring-boot-starter-log4j2:2.6.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// Configures the publishing
publishing {
    repositories {
        // The target repository
        maven {
            // Choose whatever name you want
            name = "GitHubPackages"
            // The url of the repository, where the artifacts will be published
            url = "https://maven.pkg.github.com/hugh-whitesource3/gradle-hostrules-gh-package2"
            credentials {
                // The credentials (described in the next section)
                username = System.getenv("ACTOR2")
                password = System.getenv("TOKEN2")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            // Fixes the error with dynamic versions when using Spring Boot
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}
